apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.lx.todaysbing"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 6
        versionName "1.4.0"

        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            zipAlignEnabled true
            // 移除无用的resource文件
//            shrinkResources true

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为BingWallpaper_v1.0_d2015-01-15_wandoujia.apk
                        def fileName = "Today's_Bing_v${defaultConfig.versionName}(${defaultConfig.versionCode})_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }

        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
//            applicationIdSuffix ".debug"
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "debug"]
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
    }

    productFlavors {
        umeng {}
        qh360 {}
        baidu {}
        hiapk {}
        zs91 {}
        yyb {}
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //
    compile project(':ImageViewKeepRatio-lib')
    compile project(':data-bing-library')
    compile project(':data-binggallery-library')
    //
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'com.android.support:percent:23.0.1'
    //
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.github.bumptech.glide:glide:3.6.0'
    compile 'com.commit451:PhotoView:1.2.4'
    //    compile 'com.google.guava:guava:18.0'
    compile 'com.umeng.analytics:analytics:5.5.3'
    compile 'de.greenrobot:eventbus:2.4.0'
    //    compile 'com.mcxiaoke.volley:library:1.0.16'
    compile 'de.greenrobot:greendao:1.3.7'
    compile 'com.cesards.android:cropimageview:1.0.1'
    compile project(':ShareSDK-OneKeyShare')
}

//useOldManifestMerger true